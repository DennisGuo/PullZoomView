plugins {
    id "com.android.library"
    id 'maven-publish'
    id "signing"
}

def libGroupId = 'io.github.dennisguo'
def libArtifactId = 'pullzoomview'
def libVersionName = '1.0.2'

// 从 ~/.gradle/gradle.properties 中读取配置
def ossrhUsername = findProperty('OSSRH_USERNAME')
def ossrhPassword = findProperty('OSSRH_PASSWORD')
def signingKey = findProperty('SIGNING_KEY')
def signingKeyPwd = findProperty('SIGNING_KEY_PWD')

group = libGroupId
version = libVersionName

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)

    defaultConfig {
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    api 'androidx.appcompat:appcompat:1.3.1'
    api 'androidx.recyclerview:recyclerview:1.2.1'
}


task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

signing {
    useInMemoryPgpKeys(signingKey, signingKeyPwd)
    sign publishing.publications
}

afterEvaluate {
    publishing {
        // 仓库
        repositories {
            maven {
                name = "sonatype"

                def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }
        // 发布
        publications {
            mavenJava(MavenPublication) {

                from components.release
                // You can then customize attributes of the publication as shown below.
                groupId = libGroupId
                artifactId = libArtifactId
                version = libVersionName

                artifact androidSourcesJar
//                artifact javadocJar

                pom {
                    name = libArtifactId
                    description = 'An Android custom ListView and ScrollView with pull to zoom-in.'
                    url = 'https://github.com/DennisGuo/PullZoomView'
//                    properties = []
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'DennisGuo'
                            name = 'DennisGuo'
//                        email = 'john.doe@example.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git@github.com:DennisGuo/PullZoomView.git'
                        developerConnection = 'scm:git:git@github.com:DennisGuo/PullZoomView.git'
                        url = 'https://github.com/DennisGuo/PullZoomView/'
                    }
                }
            }
        }

    }
}
